import QtQuick
import Quickshell.Io
import "Runtime.js" as Rules

Item {
  id: root
  required property string pluginDir
  // A standalone bundle already contains the exact backend it will run. Its
  // path is supplied by the native host and never goes through the plugin's
  // downloader or PATH probing.
  property string bundledExecutable: ""
  property string bundledVersion: ""
  property int bundledApiVersion: 0
  property bool bundledMode: false
  property string developmentExecutable: ""
  state: "checking"
  property string requiredVersion: ""
  property int requiredApiVersion: 0
  // One step past the pin, when the checkout has one, and the methods only
  // that step has. The handshake does not read these; `Backend` does.
  property int latestApiVersion: 0
  property var unreleasedMethods: []
  property string installedVersion: ""
  property string executable: ""
  property string error: ""
  property bool cliInstalled: false
  readonly property bool development: developmentExecutable !== ""
  readonly property bool bundled: bundledMode
  readonly property bool busy: !bundled && operation.running
  readonly property bool canInstall: !bundled && Rules.canInstall(state, busy, development)
  property string action: ""
  property string response: ""
  property bool received: false
  property bool timedOut: false
  signal validated()

  function refresh() {
    if (bundled) {
      requiredVersion = bundledVersion
      requiredApiVersion = bundledApiVersion
      latestApiVersion = bundledApiVersion
      unreleasedMethods = []
      installedVersion = bundledVersion
      executable = bundledExecutable
      error = bundledExecutable !== "" && bundledVersion !== "" && bundledApiVersion > 0
        ? "" : "Bundled backend is missing or invalid"
      state = error === "" ? "ready" : "error"
      cliInstalled = false
      if (state === "ready") validated()
      return
    }
    run("status")
  }
  function install() { if (canInstall) run("install") }
  function enableCli() { if (!development && !cliInstalled && state === "ready") run("enable-cli") }
  function disableCli() { if (!development && cliInstalled) run("disable-cli") }

  function run(command) {
    if (bundled || busy || pluginDir === "") return
    action = command
    response = ""
    received = false
    timedOut = false
    error = ""
    // A local check must not interrupt requests using the validated runtime.
    // Its answer can revoke that validation, but starting the probe cannot.
    if (command === "install" || (command === "status" && state !== "ready")) {
      executable = ""
      state = command === "install" ? "installing" : "checking"
    }
    operation.command = ["python3", pluginDir + "/scripts/backend-runtime.py", command]
    operation.running = true
    deadline.restart()
  }

  function applyResult(result, exitCode) {
    requiredVersion = result.requiredVersion
    requiredApiVersion = result.requiredApiVersion
    // A status from before the step was reported, or a harness's, has no step.
    latestApiVersion = typeof result.latestApiVersion === "number" ? result.latestApiVersion : result.requiredApiVersion
    unreleasedMethods = Array.isArray(result.unreleasedMethods) ? result.unreleasedMethods : []
    installedVersion = result.installedVersion
    executable = exitCode === 0 ? result.executable : ""
    error = result.error
    state = exitCode !== 0 && result.state === "ready" ? "error" : result.state
    cliInstalled = exitCode === 0 && state === "ready" && !development && result.cliInstalled === true
    if (exitCode !== 0 && result.state === "ready")
      error = "Backend runtime operation failed"
    if (state === "ready") validated()
  }

  Component.onCompleted: refresh()

  onBundledExecutableChanged: Qt.callLater(refresh)

  Timer {
    id: deadline
    interval: root.action === "install" ? 180000 : 15000
    onTriggered: {
      root.timedOut = true
      root.error = "Backend runtime operation timed out. Retry the check."
      if (root.action !== "enable-cli" && root.action !== "disable-cli") {
        root.state = "error"
        root.executable = ""
        root.cliInstalled = false
      }
      operation.running = false
    }
  }
  Process {
    id: operation
    stdout: SplitParser {
      onRead: data => {
        if (root.received || data.length > 16384) {
          root.response = ""
        } else root.response = data
        root.received = true
      }
    }
    onExited: function(exitCode) {
      deadline.stop()
      if (root.timedOut) return
      var result = Rules.decode(root.response)
      if ((root.action === "enable-cli" || root.action === "disable-cli") && exitCode !== 0) {
        root.error = result.error
        if (root.error === "") root.error = "Backend CLI operation failed"
        return
      }
      if (root.action === "install" && exitCode === 0 && result.state === "ready") {
        Qt.callLater(root.refresh)
        return
      }
      if (root.state === "ready" && result.state === "ready" && exitCode === 0
          && (root.requiredVersion !== result.requiredVersion || root.requiredApiVersion !== result.requiredApiVersion
              || root.executable !== result.executable)) {
        // Let the backend stop before publishing a different validated runtime,
        // so its replacement must perform a new exact-version handshake.
        root.state = "checking"
        root.executable = ""
        Qt.callLater(function() { root.applyResult(result, exitCode) })
      } else {
        root.applyResult(result, exitCode)
      }
    }
  }
}
